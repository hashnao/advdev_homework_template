pipeline {
  agent {
    kubernetes {
      label "${MAVEN_SLAVE_IMAGE}"
      cloud "openshift"
      inheritFrom "maven"
      containerTemplate(
        name: "jnlp",
        image: "${MAVEN_SLAVE_IMAGE}",
        resourceRequestMemory: "${JENKINS_SLAVE_MEMORY_REQUEST}",
        resourceLimitMemory: "${JENKINS_SLAVE_MEMORY_LIMIT}",
        resourceRequestCpu: "${JENKINS_SLAVE_CPU_REQUEST}",
        resourceLimitCpu: "${JENKINS_SLAVE_CPU_LIMIT}"
      )
    }
  }
  environment {
    version = getVersionFromPom("${CONTEXT_DIR}/pom.xml")
    groupId = getGroupIdFromPom("${CONTEXT_DIR}/pom.xml")
    artifactId = getArtifactIdFromPom("${CONTEXT_DIR}/pom.xml")
    devTag = "${version}-${BUILD_NUMBER}"
    prodTag = "${version}"
    mvnCmd = "mvn -s ./nexus_settings.xml"
    APP_NAME = "${APP_NAME}"
    APP_IMAGE = "${APP_IMAGE}"
    CLUSTER = "${CLUSTER}"
    GIT_SOURCE_URL = "${GIT_SOURCE_URL}"
    GIT_SOURCE_REF = "${GIT_SOURCE_REF}"
    MAVEN_SLAVE_IMAGE = "${MAVEN_SLAVE_IMAGE}"
    MAVEN_PATH = "${MAVEN_PATH}"
    NAMESPACE_DEV = "${NAMESPACE_DEV}"
    NAMESPACE_PROD = "${NAMESPACE_PROD}"
    NEXUS_URL = "${NEXUS_URL}"
    NEXUS_URI_MAVEN = "${NEXUS_URI_MAVEN}"
    NEXUS_URI_PUBLIC = "${NEXUS_URI_PUBLIC}"
    NEXUS_REGISTRY_URL = "${NEXUS_REGISTRY_URL}"
    NEXUS_USER = "${NEXUS_USER}"
    NEXUS_PASSWORD = "${NEXUS_PASSWORD}"
    PATH = "$PATH:${MAVEN_PATH}"
    REGISTRY_URL = "${REGISTRY_URL}"
    SONAR_URL = "${SONAR_URL}"
    TOKEN = readFile("/run/secrets/kubernetes.io/serviceaccount/token").trim()
  }
  options {
    timeout(time: 20, unit: 'MINUTES')
  }
  stages {
    stage('Variable Check') {
      steps {
        echo "version: ${version}"
        echo "devTag: ${devTag}"
        echo "prodTag: ${prodTag}"
        /*
        sh "printenv"
        sh "skopeo --version"
        sh "oc version"
        sh "which mvn"
        */
      }
    }
    stage('Check Out Source Code') {
      steps {
        git url: "${GIT_SOURCE_URL}", branch: "${GIT_SOURCE_REF}"
        sh "sed -e 's|NEXUS_URL|${NEXUS_URL}|' -e 's|NEXUS_URI_PUBLIC|${NEXUS_URI_PUBLIC}/|' -e 's|NEXUS_USER|${NEXUS_USER}|' -e 's|NEXUS_PASSWORD|${NEXUS_PASSWORD}|' ./nexus_settings.xml > ${CONTEXT_DIR}/nexus_settings.xml"
      }
    }
    stage('Build Source Code') {
      steps {
        echo "Building artifacts using Maven."
        dir("${CONTEXT_DIR}") {
          sh "${mvnCmd} clean package -DskipTests=true"
        }
      }
    }
    stage('Run Unit Test') {
      steps {
        echo "Running unit tests using Maven."
        dir("${CONTEXT_DIR}") {
          sh "${mvnCmd} test"
        }
      }
    }
    stage('Run Code Coverage Tests') {
      steps {
        echo "Running code coverage tests using SonarQube."
        dir("${CONTEXT_DIR}") {
          sh "${mvnCmd} sonar:sonar -Dsonar.host.url=${SONAR_URL} -Dsonar.projectName=${JOB_BASE_NAME}-${devTag}"
        }
      }
    }
    stage('Upload Artifacts to Nexus') {
      steps {
        echo "Archiving the artifacts to the nexus repository."
        dir("${CONTEXT_DIR}") {
          sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::${NEXUS_URL}/${NEXUS_URI_MAVEN}/"
        }
      }
    }
    stage('Build Container Image') {
      steps {
        echo "Building a container image ${devTag}."
        script {
          openshift.withCluster() {
            openshift.withProject("${NAMESPACE_DEV}") {
              if(!openshift.selector("bc", "${APP_NAME}").exists()) {
                // The selector returned from newBuild will select all objects created by the operation
                def nb = openshift.newBuild(
                  "--name=${APP_NAME}",
                  "--image-stream=${APP_IMAGE}",
                  "--binary=true",
                  "--to=${APP_NAME}:latest")
                // Print out information about the objects created by newBuild
                echo "newBuild created: ${nb.count()} objects: ${nb.names()}"
              }
              // Define nb again as the nb definition above can be applied only within the if statement.
              def nb = openshift.selector("bc", "${APP_NAME}")
              nb.startBuild("--from-dir=${CONTEXT_DIR}/target/")
              // Filter non-BuildConfig objects and create selector which will find builds related to the BuildConfig
              def buildSelector = nb.narrow("bc").related("builds")
              // Print a list of the builds which have been created
              echo "Build logs for ${buildSelector.names()}:"
              // Find the bc again, and ask for its logs
              def result = nb.narrow("bc").logs()
              // Throw exception after 5 minutes
              timeout(5) {
                buildSelector.untilEach(1) {
                  return (it.object().status.phase == "Complete")
                }
              }
              echo "Builds have been completed: ${buildSelector.names()}"
              // Each high-level operation exposes stout/stderr/status of oc actions that composed
              echo "Result of logs operation:"
              echo "  status: ${result.status}"
              echo "  stderr: ${result.err}"
              echo "  number of actions to fulfill: ${result.actions.size()}"
              echo "  first action executed: ${result.actions[0].cmd}"
              // Tag the ${APP_NAME}:latest image as ${APP_NAME}:${devTag}
              openshift.tag("${APP_NAME}:latest", "${APP_NAME}:${devTag}")
            }
          }
        }
      }
    }
    stage('Deploy Image to Dev') {
      steps {
        echo "Deploying the image in the development environment."
        script {
          sh "oc set image dc/${APP_NAME} ${APP_NAME}=${NAMESPACE_DEV}/${APP_NAME}:${devTag} --source=istag"
        }
      }
    }
    stage('Run Integration Test') {
      steps {
        echo "Running integration tests using curl."
        script {
          sh "curl -k https://${APP_NAME}-${NAMESPACE_DEV}.${CLUSTER}/ws/healthz/"
          sh "curl -k https://${APP_NAME}-${NAMESPACE_DEV}.${CLUSTER}/ws/info/"
          echo "Populating the MLB Parks Data into the MongoDB database"
          sh "curl -k https://${APP_NAME}-${NAMESPACE_DEV}.${CLUSTER}/ws/data/load/"
        }
      }
    }
    stage('Upload Image to Nexus') {
      steps {
        echo "Archiving the container image to the nexus registry."
        script {
          sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false --src-creds openshift:${TOKEN} --dest-creds ${NEXUS_USER}:${NEXUS_PASSWORD} ${REGISTRY_URL}/${NAMESPACE_DEV}/${APP_NAME}:${devTag} ${NEXUS_REGISTRY_URL}/${APP_NAME}:${devTag}"
        }
      }
    }
  }
}
// Convenience Functions to read variables from the pom.xml
// Do not change anything below this line.
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}
def getGroupIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<groupId>(.+)</groupId>'
  matcher ? matcher[0][1] : null
}
def getArtifactIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<artifactId>(.+)</artifactId>'
  matcher ? matcher[0][1] : null
}
